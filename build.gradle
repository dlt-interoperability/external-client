buildscript {
    ext {
        coroutines_version = "1.3.7"
        protobuf_version = "3.11.1"
        grpc_version = "1.29.0"
        grpc_kotlin_version = "0.1.4"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'application'
    id 'idea'
    id 'com.google.protobuf' version '0.8.12'
}

configurations.all { configuration ->
    // Workaround for dependency configuration issue:
    // https://github.com/google/protobuf-gradle-plugin/issues/391
    if (name.contains('Proto')) {
        attributes.attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage.class, Usage.JAVA_RUNTIME))
    }
}

repositories {
    jcenter()
    mavenCentral()
    // Local Maven repository
    maven {
        url 'file://Users/allirvin/.m2/repository'
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    // Arrow is a functional library for Kotlin
    implementation "io.arrow-kt:arrow-core:0.10.4"

    // gRPC and protobuf dependencies
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    // Kotlin command line client library - Clikt
    implementation "com.github.ajalt:clikt:2.7.1"

    // RSA accumulator library
    implementation "org.starcoin.rsa:rsa-accumulator:0.1"
}

application {
    mainClassName = 'external.client.AppKt'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
        languageVersion = "1.3"
        apiVersion = "1.3"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        // Specify protoc to generate using kotlin protobuf plugin
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        // Specify protoc to generate using our grpc kotlin plugin
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
}